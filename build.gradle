/*
 * sChat, a Supercharged Minecraft Chat Plugin
 * Copyright (C) Silthus <https://www.github.com/silthus>
 * Copyright (C) sChat team and contributors
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */
//file:noinspection GroovyAssignabilityCheck

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0' apply false
    id 'kr.entree.spigradle' version '2.2.4' apply false
    id 'io.freefair.lombok' version '6.3.0' apply false
    id 'org.cadixdev.licenser' version '0.6.1' apply false
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'org.cadixdev.licenser'
    apply plugin: 'jacoco'
    apply plugin: 'java-library'
    apply plugin: 'java-test-fixtures'
    apply plugin: 'idea'

    apply from: "$rootDir/gradle/jacoco.gradle"
    apply from: "$rootDir/gradle/publish.gradle"

    if (project.hasProperty("local_script")) {
        apply from: file(local_script + "/build.local.gradle")
    }

    sourceCompatibility = 17
    targetCompatibility = 17

    archivesBaseName = project.property("pluginName")
    group project.property("group")

    ext {
        mcVersion = project.property("mcVersion")
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs += ["-parameters"]
        options.fork = true
        options.forkOptions.executable = 'javac'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
        maven { url = 'https://repo.dmulloy2.net/repository/public/' }
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url = 'https://maven.enginehub.org/repo/' }
        maven { url = 'https://storehouse.okaeri.eu/repository/maven-public/' }
        maven { url = 'https://jitpack.io' }
    }

    dependencies {
        api 'net.kyori:adventure-api:4.9.3'

        implementation 'net.kyori:adventure-text-minimessage:4.10.0-SNAPSHOT'
        implementation 'net.kyori:adventure-text-serializer-plain:4.9.3'
        implementation 'net.kyori:adventure-text-serializer-gson:4.9.3'
        implementation 'net.kyori:event-api:3.0.0'

        implementation 'org.jetbrains:annotations:22.0.0'

        // Test dependencies
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
        testImplementation 'org.mockito:mockito-core:4.0.0'
        testImplementation 'org.assertj:assertj-core:3.21.0'

        testFixturesImplementation 'net.kyori:event-api:3.0.0'
        testFixturesImplementation 'org.mockito:mockito-core:4.0.0'
        testFixturesImplementation 'org.assertj:assertj-core:3.21.0'
        testFixturesImplementation 'net.kyori:adventure-text-serializer-plain:4.9.3'
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "skipped", "failed"
        }
        ignoreFailures = false
    }

    processResources {
        project.properties.put("version", this.version)
        expand project.properties
    }

    license {
        header = rootProject.file("HEADER.txt")
        include '**/*.java'
        newLine = true
    }

    tasks.build.dependsOn(updateLicenses)

    defaultTasks 'licenseFormat', 'build'
}